import com.task.TaskConsumer
import com.task.TaskProducer
import com.worker.CreateMD5
import com.dependencies.Dependencies

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'talaiot'

android {
    compileSdkVersion 28


    defaultConfig {
        applicationId "com.inaki.myapplication"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation(project(":lib1"))
    implementation(project(":lib2"))
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation Dependencies.kotlin
    implementation Dependencies.appcompat
    implementation Dependencies.constraintLayout
    testImplementation Dependencies.junit
    androidTestImplementation Dependencies.runner
    androidTestImplementation Dependencies.espresso
}

def taskProducer = tasks.register("taskProducer", TaskProducer) {
    output.set(new File(project.buildDir, "test.log"))
}

def artifact = taskProducer.flatMap { it.output }

tasks.register("taskConsumer", TaskConsumer) {
    input.set(artifact)
    showFile()
}

tasks.register("md5", CreateMD5) {
    destination = file("${buildDir}/md5")
    source file("src/main/java")
}

talaiot {
    publishers {
        taskDependencyGraphPublisher {
            html = true
        }

        influxDbPublisher {
            dbName = "tracking"
            url = "http://localhost:8086"
            urlMetric = "tracking"

        }

    }
}

